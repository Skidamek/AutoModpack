import groovy.json.JsonSlurper

pluginManagement {
    repositories {
        mavenCentral()
        mavenLocal()
        gradlePluginPortal()
        maven { url "https://maven.architectury.dev/" }
        maven { url = "https://maven.fabricmc.net/" }
        maven { url = "https://maven.quiltmc.org/repository/release" }
        maven { url "https://files.minecraftforge.net/maven/" }
        maven { url = "https://jitpack.io" }

    }
    resolutionStrategy {
        eachPlugin {
            switch (requested.id.id) {
                case "com.replaymod.preprocess": {
                    useModule("com.github.Fallen-Breath:preprocessor:${requested.version}")
                    break
                }
            }
        }
    }
}

def settings = new JsonSlurper().parseText(file('settings.json').text)
for (String version : settings.versions) {
    include(":$version")

    def proj = project(":$version")
    proj.projectDir = file("versions/$version")
    proj.buildFileName = "../../common.gradle"
}